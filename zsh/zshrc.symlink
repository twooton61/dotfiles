# shortcut to this dotfiles path is $DOTZSH
export DOTZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/rbenv.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
	echo ".."
	echo $file
  source $file
done

unset config_files

export PATH="$HOME/.bin:$PATH"
export PATH="$PATH:`yarn global bin`"
export PATH="$PATH:$HOME/Library/Python/2.7/bin"

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

[[ -n "$DISPLAY" && "$TERM" = "xterm" ]] && export TERM=xterm-256color

# load rbenv very last or the VIM 8 installation of ruby loaded by oh-my-zsh.zsh in /usr/local/bin will take priority
source "$DOTZSH/ruby/rbenv.zsh"
